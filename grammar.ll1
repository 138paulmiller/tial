START       := BODY 
            $

BODY        := STMT SEMICOLON BODY
            | EPSILON         
             $
STMT        := ASSIGN_STMT
            |  FUNC_DEF
            | EXPR 
            $

FUNC_CALL   := ID FUNC_ARGS
            $

FUNC_DEF    := DEF ID L_PAREN PARAMS R_PAREN BODY RETURN EXPR  
            $

PARAMS      := ID ID_LIST 
            | EPSILON 
            $


ID_LIST      := COMMA ID ID_LIST 
            | EPSILON 
            $



FUNC_ARGS   :=  L_PAREN ARGS R_PAREN 
            | EPSILON              
            $

ARGS        := EXPR EXPR_LIST 
            | EPSILON  
            $

ASSIGN_STMT := LET ID ASSIGN EXPR  
                  $

EXPR_LIST   := COMMA EXPR EXPR_LIST
            | EPSILON 
            $

EXPR        := TERM EXPR_OP 
            $


EXPR_OP     := ADD TERM EXPR_OP 
            | SUB TERM EXPR_OP
            | EPSILON 
            $

TERM        := FACTOR TERM_OP 
            $

TERM_OP     := MUL FACTOR TERM_OP
            | DIV FACTOR TERM_OP 
            | EPSILON 
            $
FACTOR      := NUM
            | SUB NUM
            | STR
            | FUNC_CALL
            | L_PAREN EXPR R_PAREN 
            $

NUM 	:= '[0-9]*\.?[0-9]+'          $
LET         := 'let'                      $
AND 	:= 'and'                      $
OR 	:= 'or'                       $
NOT     	:= 'not'                      $
EQUAL     	:= '=='                       $
LESS     	:= '<'                        $
L_EQUAL     := '<='                       $
GREATER     := '>'                        $
G_EQUAL     := '>='                       $
ADD     	:= '\+'                       $
L_PAREN     := '\('                       $ 
R_PAREN     := '\)'                       $ 
SUB     	:= '-'                        $
ASSIGN     	:= '='                        $ 
MUL     	:= '\*'                       $ 
DIV     	:= '/'                        $ 
IF          := 'if'                       $ 
ELSE     	:= 'else'                     $ 
RETURN      := 'return'                   $ 
WHILE     	:= 'while'                    $ 
DEF     	:= 'def'                      $   
SEMICOLON   := ';'                        $
COMMA     	:= ','                        $      
ID     	:= '[a-zA-Z_][a-zA-Z0-9_]*'   $
STR     	:= '"[a-zA-Z0-9_ ]+"'         $
NONE     	:= '[ \t\n]+'                 $
NONE        := '#[ \t\w]+[^\n]+'          $ 

